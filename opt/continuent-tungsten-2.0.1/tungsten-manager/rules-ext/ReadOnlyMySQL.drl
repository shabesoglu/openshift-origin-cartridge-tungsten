package com.continuent.tungsten.cluster.manager.policy;
 
import java.util.Map;
import java.util.Iterator;
import java.util.HashMap;
import com.continuent.tungsten.common.cluster.resource.*;
import com.continuent.tungsten.common.utils.StatisticsMap;
import com.continuent.tungsten.cluster.manager.ClusterManagementHelper;
import com.continuent.tungsten.manager.cluster.policy.ClusterPolicyManagerMode;
import com.continuent.tungsten.cluster.manager.policy.EnterprisePolicyManager;
import com.continuent.tungsten.manager.exception.ClusterManagerException;
import org.drools.runtime.rule.*;
import org.apache.log4j.Logger;

global ClusterManagementHelper cluster;
global Logger logger;
global EnterprisePolicyManager policyMgr;
global ClusterPolicyManagerMode mode; 
global Map resourceStates;

declare DataSource
	@role(event)
end

rule "MAKE DATABASE READ-ONLY WHEN MASTER GOES OFFLINE"
salience 990
	when
		onlineMaster : DataSource($dsName : name,
									$lastRole : role,
									$lastState : state,
									state == ResourceState.ONLINE,
									role == "master")
		
		notOnlineMaster : DataSource(name == $dsName,
									   (role != $lastRole || state != $lastState),
									   this after onlineMaster)
		eval(cluster.isCoordinator())
	then	
		Object[] params = {$dsName}; 
	 	logger.info(String.format("MAKING DATABASE READ-ONLY: '%s'", params));
	 	cluster.clusterServiceCommand($dsName + "/mysql_readonly", "start");
end

rule "MAKE DATABASE WRITEABLE WHEN MASTER GOES ONLINE"
salience 990
	when
		notOnlineMaster : DataSource($dsName : name,
									   (role != "master" || state != ResourceState.ONLINE))

		onlineMaster : DataSource(name == $dsName,
									state == ResourceState.ONLINE,
									role == "master",
									this after notOnlineMaster)
		eval(cluster.isCoordinator())
	then	
		Object[] params = {$dsName}; 
	 	logger.info(String.format("MAKING DATABASE WRITEABLE: '%s'", params));
	 	cluster.clusterServiceCommand($dsName + "/mysql_readonly", "stop");
end


rule "MAKE DATABASE READ-ONLY WHEN SLAVE GOES ONLINE"
salience 990
	when
		notOnlineSlave : DataSource($dsName : name,
									   (role != "slave" || state != ResourceState.ONLINE))

		onlineSlave : DataSource(name == $dsName,
									state == ResourceState.ONLINE,
									role == "slave",
									this after notOnlineSlave)
		eval(cluster.isCoordinator())
	then	
		Object[] params = {$dsName}; 
	 	logger.info(String.format("MAKING DATABASE READ-ONLY: '%s'", params));
	 	cluster.clusterServiceCommand($dsName + "/mysql_readonly", "start");
end
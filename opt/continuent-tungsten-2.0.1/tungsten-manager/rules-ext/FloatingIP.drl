package com.continuent.tungsten.cluster.manager.policy;
 
import java.util.Map;
import java.util.Iterator;
import java.util.HashMap;
import com.continuent.tungsten.common.cluster.resource.*;
import com.continuent.tungsten.common.utils.StatisticsMap;
import com.continuent.tungsten.cluster.manager.ClusterManagementHelper;
import com.continuent.tungsten.manager.cluster.policy.ClusterPolicyManagerMode;
import com.continuent.tungsten.cluster.manager.policy.EnterprisePolicyManager;
import com.continuent.tungsten.manager.exception.ClusterManagerException;
import org.drools.runtime.rule.*;
import org.apache.log4j.Logger;

global ClusterManagementHelper cluster;
global Logger logger;
global EnterprisePolicyManager policyMgr;
global ClusterPolicyManagerMode mode;
global Map resourceStates;

declare DataSource
	@role(event)
end

rule "EXECUTE 'MASTER STOP' WHEN MASTER GOES OFFLINE"
salience 990
	when
		onlineMaster : DataSource($dsName : name,
									$lastRole : role,
									$lastState : state,
									state == ResourceState.ONLINE,
									role == "master")
		
		notOnlineMaster : DataSource(name == $dsName,
									   (role != $lastRole || state != $lastState),
									   this after onlineMaster)
	then	
		Object[] params = {$dsName}; 
	 	logger.info(String.format("MASTER GOING OFFLINE: '%s'", params));
	 	cluster.clusterServiceCommand($dsName + "/master", "stop");
end

rule "EXECUTE 'MASTER START' WHEN MASTER GOES ONLINE"
salience 990
	when
		notOnlineMaster : DataSource($dsName : name,
									   (role != "master" || state != ResourceState.ONLINE))

		onlineMaster : DataSource(name == $dsName,
									state == ResourceState.ONLINE,
									role == "master",
									this after notOnlineMaster)
	then	
		Object[] params = {$dsName}; 
	 	logger.info(String.format("MASTER GOING ONLINE: '%s'", params));
	 	cluster.clusterServiceCommand($dsName + "/master", "start");
end
